/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Mathlib.Probability.Notation
import Mathlib.Probability.ConditionalProbability

/-!
# Entropy and conditional entropy

## Main definitions

* `measureEntropy`: entropy of a measure
* `entropy`: entropy of a random variable, defined as `measureEntropy (volume.map X)`
* `condEntropy`: conditional entropy of a random variable `X` w.r.t. another one `Y`

## Main statements

* `chain_rule`: `entropy (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) = entropy Y + condEntropy X Y`

-/

open Real MeasureTheory

open scoped ENNReal NNReal Topology ProbabilityTheory BigOperators

section x_log_x

/-! Properties of the function `x â†¦ x * log x`. -/

namespace Real

lemma tendsto_log_mul_nhds_zero_left :
    Filter.Tendsto (fun x â†¦ log x * x) (ğ“[<] 0) (ğ“ 0) := by
  have h := tendsto_log_mul_rpow_nhds_zero zero_lt_one
  simp only [rpow_one] at h
  have h_eq : âˆ€ x âˆˆ Set.Iio 0, (- (fun x â†¦ log x * x) âˆ˜ (fun x â†¦ |x|)) x = log x * x := by
    intro x hx
    simp only [Set.mem_Iio] at hx
    simp only [Pi.neg_apply, Function.comp_apply, log_abs]
    rw [abs_of_nonpos hx.le]
    simp only [mul_neg, neg_neg]
  refine tendsto_nhdsWithin_congr h_eq ?_
  rw [â† neg_zero]
  refine Filter.Tendsto.neg ?_
  simp only [neg_zero]
  refine h.comp ?_
  refine tendsto_abs_nhdsWithin_zero.mono_left ?_
  refine nhdsWithin_mono 0 (fun x hx â†¦ ?_)
  simp only [Set.mem_Iio] at hx
  simp only [Set.mem_compl_iff, Set.mem_singleton_iff, hx.ne, not_false_eq_true]

lemma continuous_id_mul_log : Continuous (fun x â†¦ x * log x) := by
  rw [continuous_iff_continuousAt]
  intro x
  by_cases hx : x = 0
  swap; Â· exact (continuous_id'.continuousAt).mul (continuousAt_log hx)
  rw [hx]
  have h := tendsto_log_mul_rpow_nhds_zero zero_lt_one
  simp only [rpow_one] at h
  have h' : Filter.Tendsto (fun x â†¦ log x * x) (ğ“[<] 0) (ğ“ 0) := tendsto_log_mul_nhds_zero_left
  rw [ContinuousAt, zero_mul]
  suffices Filter.Tendsto (fun x â†¦ log x * x) (ğ“ 0) (ğ“ 0) by
    exact this.congr (fun x â†¦ by rw [mul_comm])
  nth_rewrite 1 [â† nhdsWithin_univ]
  have : (Set.univ : Set â„) = Set.Iio 0 âˆª Set.Ioi 0 âˆª {0} := by
    ext x
    simp only [Set.mem_univ, Set.Iio_union_Ioi, Set.union_singleton, Set.mem_compl_iff,
      Set.mem_singleton_iff, not_true, Set.mem_insert_iff, true_iff]
    exact em _
  rw [this, nhdsWithin_union, nhdsWithin_union]
  simp only [ge_iff_le, nhdsWithin_singleton, sup_le_iff, Filter.nonpos_iff, Filter.tendsto_sup]
  refine âŸ¨âŸ¨h', hâŸ©, ?_âŸ©
  rw [Filter.tendsto_pure_left, mul_zero]
  intro s hs
  obtain âŸ¨t, hts, _, h_zero_memâŸ© := mem_nhds_iff.mp hs
  exact hts h_zero_mem

lemma differentiableOn_id_mul_log : DifferentiableOn â„ (fun x â†¦ x * log x) {0}á¶œ :=
  differentiable_id'.differentiableOn.mul differentiableOn_log

lemma deriv_id_mul_log {x : â„} (hx : x â‰  0) : deriv (fun x â†¦ x * log x) x = log x + 1 := by
  rw [deriv_mul differentiableAt_id' (differentiableAt_log hx)]
  simp only [deriv_id'', one_mul, deriv_log', ne_eq, add_right_inj]
  exact mul_inv_cancel hx

lemma deriv2_id_mul_log {x : â„} (hx : x â‰  0) : deriv^[2] (fun x â†¦ x * log x) x = xâ»Â¹ := by
  simp only [Function.iterate_succ, Function.iterate_zero, Function.comp.left_id,
    Function.comp_apply]
  suffices âˆ€á¶  y in (ğ“ x), deriv (fun x â†¦ x * log x) y = log y + 1 by
    refine (Filter.EventuallyEq.deriv_eq this).trans ?_
    rw [deriv_add_const, deriv_log x]
  suffices âˆ€á¶  y in (ğ“ x), y â‰  0 by
    filter_upwards [this] with y hy
    exact deriv_id_mul_log hy
  exact eventually_ne_nhds hx

lemma strictConvexOn_id_mul_log : StrictConvexOn â„ (Set.Ici (0 : â„)) (fun x â†¦ x * log x) := by
  refine strictConvexOn_of_deriv2_pos (convex_Ici 0) (continuous_id_mul_log.continuousOn) ?_
  intro x hx
  simp only [Set.nonempty_Iio, interior_Ici', Set.mem_Ioi] at hx
  rw [deriv2_id_mul_log hx.ne']
  positivity

lemma convexOn_id_mul_log : ConvexOn â„ (Set.Ici (0 : â„)) (fun x â†¦ x * log x) :=
  strictConvexOn_id_mul_log.convexOn

lemma id_mul_log_nonneg {x : â„} (hx : 1 â‰¤ x) : 0 â‰¤ x * log x :=
  mul_nonneg (zero_le_one.trans hx) (log_nonneg hx)

@[measurability]
lemma measurable_id_mul_log : Measurable (fun x â†¦ x * log x) :=
  measurable_id'.mul measurable_log

end Real

section aux_lemmas

-- todo: is this somewhere?
lemma integral_eq_sum {S E : Type*} [Fintype S] [MeasurableSpace S] [MeasurableSingletonClass S]
    [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
     (Î¼ : Measure S) [IsFiniteMeasure Î¼] (f : S â†’ E) :
    âˆ« x, f x âˆ‚Î¼ = âˆ‘ x, (Î¼ {x}).toReal â€¢ (f x) := by
  conv_lhs => rw [â† Measure.sum_smul_dirac Î¼]
  have hf : Integrable f Î¼ := integrable_of_fintype _ _
  have hf' : Integrable f (Measure.sum fun a â†¦ Î¼ {a} â€¢ Measure.dirac a) := by
    rwa [Measure.sum_smul_dirac Î¼]
  rw [integral_sum_measure hf']
  simp_rw [integral_smul_measure, integral_dirac]
  rw [tsum_fintype]

/-- `Î¼[|s]` is a finite measure whenever `Î¼` is finite. -/
instance cond_isFiniteMeasure {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±}
    [IsFiniteMeasure Î¼] (s : Set Î±) :
    IsFiniteMeasure (Î¼[|s]) := by
  constructor
  rw [ProbabilityTheory.cond]
  simp only [Measure.smul_toOuterMeasure, OuterMeasure.coe_smul, Pi.smul_apply, MeasurableSet.univ,
    Measure.restrict_apply, Set.univ_inter, smul_eq_mul]
  by_cases hÎ¼s : Î¼ s = 0
  Â· simp [hÎ¼s]
  Â· refine ENNReal.mul_lt_top ?_ (measure_ne_top _ _)
    simp only [ne_eq, ENNReal.inv_eq_top]
    exact hÎ¼s

lemma cond_eq_zero_of_measure_zero {Î± : Type*} {_ : MeasurableSpace Î±} {Î¼ : Measure Î±} {s : Set Î±}
    (hÎ¼s : Î¼ s = 0) :
    Î¼[|s] = 0 := by
  have : Î¼.restrict s = 0 := by simp [hÎ¼s]
  simp [ProbabilityTheory.cond, this]

end aux_lemmas

section negIdMulLog

/-- The function `x â†¦ - x * log x` from `â„` to `â„`. -/
noncomputable
def negIdMulLog (x : â„) : â„ := - x * log x

@[simp]
lemma negIdMulLog_zero : negIdMulLog (0 : â„) = 0 := by simp [negIdMulLog]

@[simp]
lemma negIdMulLog_one : negIdMulLog (1 : â„) = 0 := by simp [negIdMulLog]

lemma negIdMulLog_eq_neg : negIdMulLog = fun x â†¦ - (x * log x) := by simp [negIdMulLog]

lemma negIdMulLog_nonneg {x : â„} (h1 : 0 â‰¤ x) (h2 : x â‰¤ 1) : 0 â‰¤ negIdMulLog x := by
  rw [negIdMulLog, neg_mul_comm]
  apply mul_nonneg h1
  simp only [Left.nonneg_neg_iff]
  exact log_nonpos h1 h2

lemma concaveOn_negIdMulLog : ConcaveOn â„ (Set.Ici (0 : â„)) negIdMulLog := by
  rw [negIdMulLog_eq_neg]
  exact convexOn_id_mul_log.neg

lemma h_jensen {S : Type*} [Fintype S] {w : S â†’ â„} {p : S â†’ â„} (h0 : âˆ€ s, 0 â‰¤ w s)
    (h1 : âˆ‘ s, w s = 1) (hmem : âˆ€ s, 0 â‰¤ p s) :
    âˆ‘ s, (w s) * negIdMulLog (p s) â‰¤ negIdMulLog (âˆ‘ s, (w s) * (p s)) := by
  refine ConcaveOn.le_map_sum concaveOn_negIdMulLog ?_ h1 ?_
  Â· simp [h0]
  Â· simp [hmem]

end negIdMulLog

end x_log_x






namespace ProbabilityTheory

variable {Î© S T : Type*} [mÎ© : MeasureSpace Î©] [IsProbabilityMeasure (â„™ : Measure Î©)]
  [Fintype S] [Fintype T] [MeasurableSpace S] [MeasurableSpace T]

section measureEntropy

variable {Î¼ : Measure S}

/-- Entropy of a measure on a finite measurable space.

We normalize the measure by `(Î¼ Set.univ)â»Â¹` to extend the entropy definition to finite measures.
What we really want to do is deal with `Î¼=0` or `IsProbabilityMeasure Î¼`, but we don't have
a typeclass for that (we could create one though).
The added complexity in the expression is not an issue because if `Î¼` is a probability measure,
a call to `simp` will simplify `(Î¼ Set.univ)â»Â¹ â€¢ Î¼` to `Î¼`. -/
noncomputable
def measureEntropy (Î¼ : Measure S) : â„ := âˆ‘ s, negIdMulLog (((Î¼ Set.univ)â»Â¹ â€¢ Î¼) {s}).toReal

@[simp]
lemma measureEntropy_zero : measureEntropy (0 : Measure S) = 0 := by
  simp [measureEntropy]

lemma measureEntropy_of_not_isFiniteMeasure (h : Â¬ IsFiniteMeasure Î¼) :
    measureEntropy Î¼ = 0 := by
  simp [measureEntropy, not_isFiniteMeasure_iff.mp h]

lemma measureEntropy_of_isProbabilityMeasure (Î¼ : Measure S) [IsProbabilityMeasure Î¼] :
    measureEntropy Î¼ = âˆ‘ s, negIdMulLog (Î¼ {s}).toReal := by
  simp [measureEntropy]

lemma measureEntropy_univ_smul : measureEntropy ((Î¼ Set.univ)â»Â¹ â€¢ Î¼) = measureEntropy Î¼ := by
  by_cases hÎ¼_fin : IsFiniteMeasure Î¼
  swap
  Â· rw [measureEntropy_of_not_isFiniteMeasure hÎ¼_fin]
    rw [not_isFiniteMeasure_iff] at hÎ¼_fin
    simp [hÎ¼_fin]
  cases eq_zero_or_neZero Î¼ with
  | inl hÎ¼ => simp [hÎ¼]
  | inr hÎ¼ =>
    rw [measureEntropy]
    simp only [Measure.smul_toOuterMeasure, OuterMeasure.coe_smul, Pi.smul_apply, smul_eq_mul,
      ENNReal.toReal_mul]
    rw [ENNReal.inv_mul_cancel]
    Â· simp only [inv_one, ENNReal.one_toReal, one_mul]
      simp [measureEntropy]
    Â· simp [hÎ¼.out]
    Â· exact measure_ne_top _ _

lemma measureEntropy_nonneg (Î¼ : Measure S) : 0 â‰¤ measureEntropy Î¼ := by
  by_cases hÎ¼_fin : IsFiniteMeasure Î¼
  swap; Â· rw [measureEntropy_of_not_isFiniteMeasure hÎ¼_fin]
  refine Finset.sum_nonneg (fun s _ â†¦ negIdMulLog_nonneg ENNReal.toReal_nonneg ?_)
  refine ENNReal.toReal_le_of_le_ofReal zero_le_one ?_
  rw [ENNReal.ofReal_one]
  cases eq_zero_or_neZero Î¼ with
  | inl hÎ¼ => simp [hÎ¼]
  | inr hÎ¼ => exact prob_le_one

lemma measureEntropy_le_card_aux (Î¼ : Measure S) [IsProbabilityMeasure Î¼] :
    measureEntropy Î¼ â‰¤ log (Fintype.card S) := by
  cases isEmpty_or_nonempty S with
  | inl h =>
    have : Î¼ = 0 := Subsingleton.elim _ _
    simp [Fintype.card_eq_zero, this]
  | inr h =>
    set N := Fintype.card S with hN
    have : 0 < N := Fintype.card_pos
    rw [measureEntropy]
    simp only [measure_univ, inv_one, one_smul]
    sorry -- use h_jensen, see pfr repo

lemma measureEntropy_le_log_card (Î¼ : Measure S) :
    measureEntropy Î¼ â‰¤ log (Fintype.card S) := by
  have h_log_card_nonneg : 0 â‰¤ log (Fintype.card S) := by
    cases isEmpty_or_nonempty S with
    | inl h =>
      rw [Fintype.card_eq_zero]
      simp
    | inr h =>
      refine log_nonneg ?_
      simp only [Nat.one_le_cast]
      exact Fintype.card_pos
  cases eq_zero_or_neZero Î¼ with
  | inl hÎ¼ =>
    simp only [hÎ¼, measureEntropy_zero]
    exact h_log_card_nonneg
  | inr hÎ¼ =>
    by_cases hÎ¼_fin : IsFiniteMeasure Î¼
    swap;
    Â· rw [measureEntropy_of_not_isFiniteMeasure hÎ¼_fin]
      exact h_log_card_nonneg
    rw [â† measureEntropy_univ_smul]
    exact measureEntropy_le_card_aux _

end measureEntropy

section entropy

variable {X : Î© â†’ S}

/-- Entropy of a random variable with values in a finite measurable space. -/
noncomputable
def entropy (X : Î© â†’ S) : â„ := measureEntropy (volume.map X)

lemma entropy_nonneg (X : Î© â†’ S) : 0 â‰¤ entropy X := measureEntropy_nonneg _

lemma entropy_le_log_card (X : Î© â†’ S) : entropy X â‰¤ log (Fintype.card S) :=
  measureEntropy_le_log_card _

end entropy

section condEntropy

variable {X : Î© â†’ S} {Y : Î© â†’ T}

/-- Conditional entropy of a random variable w.r.t. another.
This is the expectation under the law of `Y` of the entropy of the law of `X` conditioned on the
event `Y = y`. -/
noncomputable
def condEntropy (X : Î© â†’ S) (Y : Î© â†’ T) : â„ :=
  --âˆ‘ y, (volume.map Y {y}).toReal * measureEntropy ((volume[|Y â»Â¹' {y}]).map X)
  (volume.map Y)[fun y â†¦ measureEntropy ((volume[|Y â»Â¹' {y}]).map X)]

lemma condEntropy_nonneg (X : Î© â†’ S) (Y : Î© â†’ T) : 0 â‰¤ condEntropy X Y :=
  integral_nonneg (fun _ â†¦ measureEntropy_nonneg _)

lemma condEntropy_le_log_card (X : Î© â†’ S) (Y : Î© â†’ T) (hY : Measurable Y) :
    condEntropy X Y â‰¤ log (Fintype.card S) := by
  have : âˆ€ y, measureEntropy ((volume[|Y â»Â¹' {y}]).map X) â‰¤ log (Fintype.card S) :=
    fun y â†¦ measureEntropy_le_log_card _
  refine (integral_mono_of_nonneg ?_ (integrable_const (log (Fintype.card S))) ?_).trans ?_
  Â· exact ae_of_all _ (fun _ â†¦ measureEntropy_nonneg _)
  Â· exact ae_of_all _ this
  Â· have : IsProbabilityMeasure (volume.map Y) := isProbabilityMeasure_map hY.aemeasurable
    simp

lemma measureEntropy_cond_map (hX : Measurable X) (y : T) :
    measureEntropy ((volume[|Y â»Â¹' {y}]).map X)
      = âˆ‘ x, negIdMulLog ((â„™[|Y â»Â¹' {y}]).map X {x}).toReal := by
  by_cases hy : â„™ (Y â»Â¹' {y}) = 0
  Â· rw [cond_eq_zero_of_measure_zero hy]
    simp
  Â· have : IsProbabilityMeasure (volume[|Y â»Â¹' {y}]) := cond_isProbabilityMeasure _ hy
    have : IsProbabilityMeasure ((volume[|Y â»Â¹' {y}]).map X) :=
      isProbabilityMeasure_map hX.aemeasurable
    rw [measureEntropy]
    simp

lemma condEntropy_eq_sum [MeasurableSingletonClass T] (X : Î© â†’ S) (Y : Î© â†’ T) :
    condEntropy X Y
      = âˆ‘ y, (volume.map Y {y}).toReal * measureEntropy ((volume[|Y â»Â¹' {y}]).map X) := by
  rw [condEntropy, integral_eq_sum]
  simp_rw [smul_eq_mul]

lemma condEntropy_eq_sum_sum [MeasurableSingletonClass T] (hX : Measurable X) (Y : Î© â†’ T) :
    condEntropy X Y
      = âˆ‘ y, âˆ‘ x, (volume.map Y {y}).toReal * negIdMulLog ((â„™[|Y â»Â¹' {y}]).map X {x}).toReal := by
  rw [condEntropy_eq_sum]
  congr with y
  rw [measureEntropy_cond_map hX, mul_comm, Finset.sum_mul]
  simp_rw [mul_comm (volume.map Y {y}).toReal]

lemma condEntropy_eq_sum_prod [MeasurableSingletonClass T] (hX : Measurable X) (Y : Î© â†’ T) :
    condEntropy X Y = âˆ‘ p : S Ã— T,
      (volume.map Y {p.2}).toReal * negIdMulLog ((â„™[|Y â»Â¹' {p.2}]).map X {p.1}).toReal := by
  rw [condEntropy_eq_sum_sum hX Y]
  have h_prod : (Finset.univ : Finset (S Ã— T)) = (Finset.univ : Finset S) Ã—Ë¢ Finset.univ := rfl
  rw [h_prod, Finset.sum_product_right]

end condEntropy

section pair

variable {X : Î© â†’ S} {Y : Î© â†’ T}

lemma measure_prod_singleton_eq_mul [MeasurableSingletonClass S] [MeasurableSingletonClass T]
    (hX : Measurable X) (hY : Measurable Y) {p : S Ã— T} :
    (volume.map (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) {p}).toReal
      = (volume.map Y {p.2}).toReal * ((volume[|Y â»Â¹' {p.2}]).map X {p.1}).toReal := by
  have hp_prod : {p} = {p.1} Ã—Ë¢ {p.2} := by simp
  rw [Measure.map_apply (hX.prod_mk hY) (measurableSet_singleton p)]
  by_cases hpY : â„™ (Y â»Â¹' {p.2}) = 0
  Â· rw [cond_eq_zero_of_measure_zero hpY]
    simp only [aemeasurable_zero_measure, not_true, Measure.map_zero, Measure.zero_toOuterMeasure,
      OuterMeasure.coe_zero, Pi.zero_apply, ENNReal.zero_toReal, mul_zero]
    suffices (â„™ ((fun a â†¦ (X a, Y a)) â»Â¹' ({p.1} Ã—Ë¢ {p.2}))).toReal = 0 by convert this
    rw [Set.mk_preimage_prod, ENNReal.toReal_eq_zero_iff]
    exact Or.inl (measure_mono_null (Set.inter_subset_right _ _) hpY)
  rw [Measure.map_apply hY (measurableSet_singleton p.2)]
  simp_rw [Measure.map_apply hX (measurableSet_singleton _)]
  simp_rw [cond_apply _ (hY (measurableSet_singleton _))]
  rw [ENNReal.toReal_mul, â† mul_assoc, ENNReal.toReal_inv, mul_inv_cancel, one_mul, hp_prod,
    Set.mk_preimage_prod, Set.inter_comm]
  rw [ENNReal.toReal_ne_zero]; exact âŸ¨hpY, measure_ne_top _ _âŸ©

lemma negIdMulLog_measure_prod_singleton [MeasurableSingletonClass S] [MeasurableSingletonClass T]
    (hX : Measurable X) (hY : Measurable Y) {p : S Ã— T} :
    negIdMulLog (volume.map (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) {p}).toReal
      = - ((volume[|Y â»Â¹' {p.2}]).map X {p.1}).toReal
          * (volume.map Y {p.2}).toReal* log (volume.map Y {p.2}).toReal
        - (volume.map Y {p.2}).toReal * ((volume[|Y â»Â¹' {p.2}]).map X {p.1}).toReal
          * log ((volume[|Y â»Â¹' {p.2}]).map X {p.1}).toReal := by
  rw [measure_prod_singleton_eq_mul hX hY]
  by_cases hpY : â„™ (Y â»Â¹' {p.2}) = 0
  Â· rw [cond_eq_zero_of_measure_zero hpY]
    simp
  by_cases hpX : (volume[|Y â»Â¹' {p.2}]).map X {p.1} = 0
  Â· simp [hpX]
  rw [negIdMulLog, log_mul]
  Â· ring
  Â· rw [ENNReal.toReal_ne_zero]
    refine âŸ¨?_, measure_ne_top _ _âŸ©
    rwa [Measure.map_apply hY (measurableSet_singleton _)]
  Â· rw [ENNReal.toReal_ne_zero]
    refine âŸ¨hpX, measure_ne_top _ _âŸ©

lemma chain_rule [MeasurableSingletonClass S] [MeasurableSingletonClass T]
    (hX : Measurable X) (hY : Measurable Y) :
    entropy (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) = entropy Y + condEntropy X Y := by
  have : IsProbabilityMeasure (volume.map Y) := isProbabilityMeasure_map hY.aemeasurable
  have : IsProbabilityMeasure (volume.map (fun Ï‰ â†¦ (X Ï‰, Y Ï‰))) :=
    isProbabilityMeasure_map (hX.prod_mk hY).aemeasurable
  rw [entropy, measureEntropy_of_isProbabilityMeasure]
  simp_rw [negIdMulLog_measure_prod_singleton hX hY, sub_eq_add_neg, Finset.sum_add_distrib]
  congr
  Â· have h_prod : (Finset.univ : Finset (S Ã— T)) = (Finset.univ : Finset S) Ã—Ë¢ Finset.univ := rfl
    rw [h_prod, Finset.sum_product_right, entropy, measureEntropy_of_isProbabilityMeasure]
    congr with y
    simp only [neg_mul, Finset.sum_neg_distrib]
    rw [â† Finset.sum_mul, â† Finset.sum_mul]
    by_cases hy : â„™ (Y â»Â¹' {y}) = 0
    Â· simp [cond_eq_zero_of_measure_zero hy, Measure.map_apply hY, hy]
    have : IsProbabilityMeasure (volume[|Y â»Â¹' {y}]) := cond_isProbabilityMeasure _ hy
    suffices âˆ‘ x : S, (Measure.map X (â„™[|Y â»Â¹' {y}]) {x}).toReal = 1 by
      rw [this, one_mul, â† neg_mul, negIdMulLog]
    rw [â† ENNReal.toReal_sum (fun _ _ â†¦ measure_ne_top _ _), ENNReal.toReal_eq_one_iff]
    simp_rw [Measure.map_apply hX (measurableSet_singleton _)]
    rw [sum_measure_preimage_singleton _ (fun y _ â†¦ hX (measurableSet_singleton y))]
    simp
  Â· rw [condEntropy_eq_sum_prod hX]
    congr with p
    rw [negIdMulLog]
    ring

end pair

end ProbabilityTheory
